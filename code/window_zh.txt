窗口(80, 80)

屏幕 := 整[80, 80, 3]

函数 画飞机(屏幕, y, x) {
    黑色 := [0, 0, 0]
    屏幕[y, x] = 屏幕[y, x + 1] = 屏幕[y, x + 2] = 屏幕[y + 1, x + 1] = 黑色
}

炸弹 := 整[100, 2]
炸弹数量 := 0

函数 添加炸弹(x, y) {
    炸弹[炸弹数量] = [y + 2, x + 1]
    炸弹数量 = (炸弹数量 + 1) % 100
}

函数 更新炸弹(边界) {
    i := 0
    当 i < 炸弹数量 {
        若 炸弹[i, 0] < 边界 {
            炸弹[i, 0] = 炸弹[i, 0] + 1
        }
        若 炸弹[i, 0] == 边界 {
            j := i
            当 j < 炸弹数量 - 1 {
                炸弹[j] = 炸弹[j + 1]
                j = j + 1
            }
            i = i - 1
            炸弹数量 = 炸弹数量 - 1
        }
        i = i + 1
    }
}

函数 画炸弹(屏幕, 边界) {
    i := 0
    当 i < 炸弹数量 {
        若 炸弹[i, 0] < 边界 {
            屏幕[炸弹[i, 0], 炸弹[i, 1]] = [0, 0, 0]
        }
        i = i + 1
    }
}

// 做一个白色背景
白色 := [255, 255, 255]
背景 := int[80, 80, 3]
i := 0
当 i < 80 {
    j := 0
    当 j < 80 {
        背景[i, j] = 白色
        j = j + 1
    }
    i = i + 1
}

x := 40
y := 40

// 主循环
上一刻 := 用时()
当 KEYBOARD[KEY_ESC] != KEY_DOWN {
    此时 := 用时()
    若 (此时 - 上一刻) < 33 {
        过
    }
    上一刻 = 此时

    // 获取输入
    若 KEYBOARD[KEY_W] == KEY_DOWN 并 y < 78 {
        y = y + 1
    }
    若 KEYBOARD[KEY_S] == KEY_DOWN 并 y > 0 {
        y = y - 1
    }
    若 KEYBOARD[KEY_A] == KEY_DOWN 并 x > 0 {
        x = x - 1
    }
    若 KEYBOARD[KEY_D] == KEY_DOWN 并 x < 77 {
        x = x + 1
    }
    若 KEYBOARD[KEY_SPACE] == KEY_DOWN {
        添加炸弹(x, y)
    }

    // 渲染
    屏幕 = 背景
    画飞机(屏幕, y, x)
    更新炸弹(80)
    画炸弹(屏幕, 80)
    WINDOW = 屏幕
}
